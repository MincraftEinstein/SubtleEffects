plugins {
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle'
    id 'org.spongepowered.mixin'
}

base {
    archivesName = "${mod_name}-forge-${minecraft_version}"
}

mixin {
    add(sourceSets.main, "${mod_id}.refmap.json")
    config("${mod_id}.mixins.json")
    config("${mod_id}.forge.mixins.json")
}

minecraft {
    mappings channel: 'official', version: minecraft_version

    accessTransformer = project(":common").file('src/main/resources/META-INF/accesstransformer.cfg')

    copyIdeResources = true

    runs {
        client {
            workingDirectory project.file('runs/client')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Client'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modClientRun {
                    source sourceSets.main
                    source project(":common").sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('runs/server')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Server'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modServerRun {
                    source sourceSets.main
                    source project(":common").sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

repositories {
    maven {
        url = "https://thedarkcolour.github.io/KotlinForForge/"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    compileOnly project(":common")
    annotationProcessor("org.spongepowered:mixin:0.8.7-SNAPSHOT:processor")
    implementation(fg.deobf("me.fzzyhmstrs:fzzy_config:${fzzy_config_version}+forge"))
    implementation("thedarkcolour:kotlinforforge:4.11.0")
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:0.3.5")) {
        jarJar.ranged(it, "[0.3.5,)")
    }
    implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:0.3.5"))
    compileOnly "com.github.glitchfiend:GlitchCore-forge:${glitchcore_version}"
    compileOnly "com.github.glitchfiend:SereneSeasons-forge:${serene_seasons_version}"
    compileOnly fg.deobf("it.crystalnest:cobweb-forge:${cobweb_version}")
    compileOnly fg.deobf("it.crystalnest:soul-fire-d-forge:${soul_fired_version}")
    compileOnly("maven.modrinth:iceberg:${forge_iceberg_version}")
    compileOnly("maven.modrinth:prism-lib:${forge_prism_version}")
    compileOnly("maven.modrinth:item-borders:${forge_item_borders_version}")
    compileOnly("maven.modrinth:endrem:${forge_endrem_version}")
    implementation fg.deobf("maven.modrinth:moonlight:${forge_moonlight_version}")
    implementation fg.deobf("maven.modrinth:immersive-weathering:${forge_immersive_weathering_version}")
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}

processResources {
    from project(":common").sourceSets.main.resources

    exclude("subtle_effects.accesswidener")
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
            fg.component(it)
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}