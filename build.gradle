plugins {
    id 'fabric-loom' version '1.9-SNAPSHOT' apply(false)
    id 'net.minecraftforge.gradle' version '[6.0,6.2)' apply(false)
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT' apply(false)
    id("org.spongepowered.mixin") version "0.7-SNAPSHOT" apply(false)
}

subprojects {
    apply plugin: 'java'

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)

    jar {
        manifest {
            attributes([
                    'Specification-Title'   : mod_name,
                    'Specification-Vendor'  : mod_author,
                    'Specification-Version' : project.jar.archiveVersion,
                    'Implementation-Title'  : project.name,
                    'Implementation-Version': project.jar.archiveVersion,
                    'Implementation-Vendor' : mod_author,
                    'Built-On-Minecraft'    : minecraft_version
            ])
        }
    }

    repositories {
        mavenCentral()
        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }

        maven {
            name = 'BlameJared Maven (JEI / CraftTweaker / Bookshelf)'
            url = 'https://maven.blamejared.com'
        }

        maven {
            name = "FzzyMaven"
            url = "https://maven.fzzyhmstrs.me/"
        }

        maven {
            name = "Fuzs Mod Resources"
            url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
        }

        maven {
            name = "Crystal Nest"
            url = "https://maven.crystalnest.it"
        }

        maven {
            name = 'Forge'
            url = 'https://maven.minecraftforge.net/'
        }

        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
        }

        maven {
            name = "Fuzs Mod Resources"
            url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.getRelease().set(17)
    }

    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }

    processResources {
        def expandProps = [
                'version'                   : version,
                'group'                     : project.group, //Else we target the task's group.
                'minecraft_version'         : minecraft_version,
                'max_minecraft_version'     : max_minecraft_version,
                'fabric_api_version'        : fabric_api_version,
                'fabric_version'            : fabric_version,
                'mod_name'                  : mod_name,
                'mod_author'                : mod_author,
                'mod_id'                    : mod_id,
                'description'               : project.description,
                'forge_version'             : forge_version,
                'forge_loader_version_range': forge_loader_version_range,
                'fzzy_config_version'       : fzzy_config_version
        ]

        filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', 'META-INF/neoforge.mods.toml']) {
            expand expandProps
        }

        inputs.properties(expandProps)
    }
}